{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "9ad287e0",
   "metadata": {},
   "source": [
    "# Euler 4\n",
    "\n",
    "A palindromic number reads the same both ways. The largest palindrome made from the product of two 2-digit numbers is 9009 = 91 * 99.\n",
    "\n",
    "Find the largest palindrome made from the product of two 3-digit numbers.\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "360b9ee5",
   "metadata": {
    "vscode": {
     "languageId": "j"
    }
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\n",
      "┌───┬───┐\n",
      "│100│999│\n",
      "└───┴───┘\n"
     ]
    }
   ],
   "source": [
    "threeDigitNumbers =: 100 + i. 900\n",
    "pretty =: <\"0 @(<./ , >./)\n",
    "pretty threeDigitNumbers"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "60ef714e",
   "metadata": {
    "vscode": {
     "languageId": "j"
    }
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "┌─────┬──────┐\n",
      "│10000│998001│\n",
      "└─────┴──────┘\n"
     ]
    }
   ],
   "source": [
    "products =: ~. @, @(*/ ~) threeDigitNumbers\n",
    "pretty products"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "33c5429b",
   "metadata": {
    "vscode": {
     "languageId": "j"
    }
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "01\n"
     ]
    }
   ],
   "source": [
    "reverse =: |. @ \":\n",
    "reverse 10"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "id": "f6881da0",
   "metadata": {
    "vscode": {
     "languageId": "j"
    }
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "0 1 1 1 1\n"
     ]
    }
   ],
   "source": [
    "isPalindrome =: (reverse -: \":)\"0\n",
    "isPalindrome 10 11 121 131 9009"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "id": "cfa75304",
   "metadata": {
    "vscode": {
     "languageId": "j"
    }
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "11 121\n"
     ]
    }
   ],
   "source": [
    "filterPalindromes =: (isPalindrome # ]) \n",
    "filterPalindromes 10 11 12 13 121"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "id": "6fe55bb4",
   "metadata": {
    "vscode": {
     "languageId": "j"
    }
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "906609\n"
     ]
    }
   ],
   "source": [
    ">./ filterPalindromes products"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "id": "7ba8fa32",
   "metadata": {
    "vscode": {
     "languageId": "j"
    }
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "906609\n"
     ]
    }
   ],
   "source": [
    ">./ ((((|. @ \":) -: \":)\"0 # ]) ~. @, @(*/ ~) 100 + i. 900)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "J",
   "language": "J",
   "name": "jkernel"
  },
  "language_info": {
   "file_extension": ".ijs",
   "mimetype": "text/J",
   "name": "J"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
